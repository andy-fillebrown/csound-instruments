
opcode udo__read_midi_controller, i, iS
    i_controller_number, S_channel xin
    S_channel__read_midi strcat S_channel, "__read_midi"
    i_read_midi chnget S_channel__read_midi
    if (1 == i_read_midi) then
    	i_value_midi midictrl i_controller_number
    	chnset i_value_midi, S_channel
    endif
	i_value chnget S_channel
    xout i_value
endop


opcode udo__read_midi_controller, k, iS
    i_controller_number, S_channel xin
    S_channel__read_midi strcat S_channel, "__read_midi"
    k_read_midi chnget S_channel__read_midi
    if (1 == k_read_midi) then
    	k_value_midi midictrl i_controller_number
    	chnset k_value_midi, S_channel
    endif
	k_value_channel chnget S_channel
	k_value port k_value_channel, 0.05
    xout k_value
endop


opcode udo__read_midi_pitchbend, k, S
    S_channel xin
    S_channel__read_midi strcat S_channel, "__read_midi"
    k_read_midi chnget S_channel__read_midi
    if (1 == k_read_midi) then
        k_value_midi init 8191
		READ_MIDI_PITCHBEND:
		k_midi_in_status, k_, k_data1, k_data2 midiin
		if (0 != k_midi_in_status) then
			if (224 == k_midi_in_status) then
				k_value_midi = (128 * k_data1) + k_data2
			else
				kgoto READ_MIDI_PITCHBEND
			endif
		endif
		chnset k_value_midi, S_channel
    endif
    k_value_channel chnget S_channel
    k_value port k_value_channel, 0.05
    xout k_value
endop
